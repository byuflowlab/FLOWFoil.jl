"""
    LegacyXfoil <: Method

In general usage, this struct is generated by calling the LegacyXfoil constructor.

# Fields
- `reynolds`: Reynolds number
- `mach`: Mach number
- `xtrip`: forced transition x/c locations on top/bottom sides
- `reinit`: reinitialize the solution? (rather than use the previous solution)
- `iter`: Maximum iterations for viscous analyses
- `npan`: Number of panels
- `percussive_maintenance`: Call `do_percussive_maintenance` upon
   convergence failure?
- `printdata`: Print data obtained from XFOIL during the solution?
- `zeroinit`: Start angle of attack sweeps from zero?  If `true`, results will be sorted by ascending angle of attack.
- `clmaxstop`: Stop if lift coefficient decreases twice consecutively going up?
- `clminstop`: Stop if lift coefficient increases twice consecutively going down?
"""
struct LegacyXfoil{Tre,Tma,Txt,Tri,Tit,Tnp,Tpm,Tpd,Tzi,Tcmx,Tcmn} <: Method
    reynolds::Tre
    mach::Tma
    xtrip::Txt
    reinit::Tri
    iter::Tit
    npan::Tnp
    percussive_maintenance::Tpm
    printdata::Tpd
    zeroinit::Tzi
    clmaxstop::Tcmx
    clminstop::Tcmn
end

"""
    LegacyXfoil(
        reynolds,
        mach=0.0;
        xtrip=(1.0, 1.0),
        reinit=false,
        iter=50,
        npan=140,
        percussive_maintenance=!reinit,
        printdata=false,
        zeroinit=true,
        clmaxstop=false,
        clminstop=false,
    )

Constructor for LegacyXfoil type.

# Arguments
- `reynolds::Float=1e6` : Reynolds number

# Default Arguments
- `mach::Float=0.0` : Mach number

# Keyword Arguments
- `xtrip::Tuple{Float, Float}=(1.0,1.0)`: forced transition x/c locations on top/bottom sides
- `reinit::Bool=false`: reinitialize the solution? (rather than use the previous solution)
- `iter::Int=50`: Maximum iterations for viscous analyses
- `npan::Int=140`: Number of panels
- `percussive_maintenance::Bool=!reinit`: Call `do_percussive_maintenance` upon convergence failure?
- `printdata::Bool=false`: Print data obtained from XFOIL during the solution?
- `zeroinit::Bool=true`: Start angle of attack sweeps from zero?  If `true`, results will be sorted by ascending angle of attack.
- `clmaxstop::Bool=false`: Stop if lift coefficient decreases twice consecutively going up?
- `clminstop::Bool=false`: Stop if lift coefficient increases twice consecutively going down?

# Returns
- `method::LegacyXfoil` : LegacyXfoil method object
"""
function LegacyXfoil(
    reynolds,
    mach=0.0;
    xtrip=(1.0, 1.0),
    reinit=false,
    iter=50,
    npan=140,
    percussive_maintenance=!reinit,
    printdata=false,
    zeroinit=true,
    clmaxstop=false,
    clminstop=false,
)
    return LegacyXfoil(
        reynolds,
        mach,
        xtrip,
        reinit,
        iter,
        npan,
        percussive_maintenance,
        printdata,
        zeroinit,
        clmaxstop,
        clminstop,
    )
end

"""
    LegacyXFOutputs

# Fields
- `cl::Vector{Float}`
- `cd::Vector{Float}`
- `cdp::Vector{Float}`
- `cm::Vector{Float}`
- `converged::Vector{Bool}`
"""
@kwdef struct LegacyXFOutputs{Tcl,Tcd,Tcdp,Tcm,Tconv}
    cl::Tcl
    cd::Tcd
    cdp::Tcdp
    cm::Tcm
    converged::Tconv
end

"""
    analyze_lxf(coordinates, flow_angles; method=LegacyXfoil())

Analysis function for LegacyXfoil.

# Arguments
- `coordinates::Matrix{Float}` : [x y] coordinates
- `alpha::Float` : angle of attack at which to analyze

# Keyword Arguments
- `method::LegacyXfoil` : A LegacyXfoil method object.

# Returns
- `outputs::LegacyXFOutputs` : outputs object with fields:
    - `cl::AbstractArray{Float}` : coefficients of lift
    - `cd::AbstractArray{Float}` : coefficient of drag
    - `cdp::AbstractArray{Float}` : coefficient of pressure drag
    - `cm::AbstractArray{Float}` : coefficient of moments
    - `converged::Bool` : whether xfoil converged on a solution
"""
function analyze_lxf(coordinates, alpha; method=LegacyXfoil())

    # - Run LegacyXfoil - #
    cl, cd, cdp, cm, converged = xf.alpha_sweep(
        reverse(coordinates[:, 1]),
        reverse(coordinates[:, 2]),
        alpha,
        method.reynolds;
        method.mach,
        method.xtrip,
        method.reinit,
        method.iter,
        method.npan,
        method.percussive_maintenance,
        method.printdata,
        method.zeroinit,
        method.clmaxstop,
        method.clminstop,
    )

    # - Return Outputs - #
    return LegacyXFOutputs(cl, cd, cdp, cm, converged)
end
